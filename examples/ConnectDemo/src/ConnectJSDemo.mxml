<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical" applicationComplete="init()">
	<mx:Script>
		<![CDATA[
			import com.facebook.data.users.GetInfoData;
			import com.facebook.session.WebSession;
			import com.facebook.commands.auth.GetSession;
			import com.facebook.commands.friends.GetFriends;
			import com.facebook.commands.users.GetInfo;
			import com.facebook.data.auth.ExtendedPermissionValues;
			import com.facebook.data.friends.GetFriendsData;
			import com.facebook.data.JSONResultData;
			import com.facebook.data.users.GetInfoFieldValues;
			import com.facebook.data.users.FacebookUser;
			import com.facebook.delegates.IFacebookCallDelegate;
			import com.facebook.events.FacebookEvent;
			import com.facebook.Facebook;
			import com.facebook.net.FacebookCall;
			import com.facebook.utils.FacebookConnectUtil;
			import com.facebook.utils.FacebookSessionUtil;
			import com.facebook.session.JSSession;
			import mx.controls.Alert;
			
			protected const RSS_URL:String = 'http://blogs.adobe.com/index.xml';
			
			protected var connect:FacebookConnectUtil;
			protected var facebook:Facebook;
			
			protected function init():void {
				connect = new FacebookConnectUtil(loaderInfo);
				facebook = new Facebook();
				
				//Start a WebSession with the ss and session key passed in from Javascript
				var webSession:WebSession = new WebSession('YOUR_API_KEY', loaderInfo.parameters.secret, loaderInfo.parameters.session_key);
				facebook.startSession(webSession);
				
				webSession.addEventListener(FacebookEvent.CONNECT, onSessionConnect);
				webSession.verifySession();
				
				//Add Events that come from each renderer. 
				rssList.addEventListener('post', postToFacebook);
				rssList.addEventListener('share', shareLink);
				
				var permsDP:Array = [ExtendedPermissionValues.SHARE_ITEM, 
					ExtendedPermissionValues.CREATE_EVENT, ExtendedPermissionValues.CREATE_NOTE, ExtendedPermissionValues.EMAIL,
					ExtendedPermissionValues.OFFLINE_ACCESS, ExtendedPermissionValues.PHOTO_UPLOAD, ExtendedPermissionValues.RSVP_EVENT,
					ExtendedPermissionValues.SMS, ExtendedPermissionValues.STATUS_UPDATE
				];
				permsCB.dataProvider = permsDP;
				
				friendsList.labelFunction = formatFriendsLabel;
				
				rssService.url = RSS_URL;
				rssService.send();
			}
			
			protected function onServiceResult():void {
				if (rssService.lastResult == null) {
					rssList.dataProvider = [];
					Alert.show('Error loading RSS feed.');
					return;
				}
				
				var results:XMLList = rssService.lastResult..item;
				if (results.length() == 0) { rssList.dataProvider = []; return; }
				rssList.dataProvider = results;
			}
			
			protected function onSessionConnect(event:FacebookEvent):void {
				if (event.success) {
					var call:FacebookCall = facebook.post(new GetFriends());
					call.addEventListener(FacebookEvent.COMPLETE, onFriendsLoad);
					
					facebook.post(call);
				} else {
					//Javascript will handle login here.
				}
			}
			
			protected function onFriendsLoad(event:FacebookEvent):void {
				/**
					If we get a success of true back the call was completed sucesfully.
					Now we make a second call to get more detailed information for each friends.
					 * We limit to the top 100 for speed reasons, if you wish you can request all of your friends details.
				 */
				if (event.success) {	
					var friends:Array = (event.data as GetFriendsData).friends.source;
					
					var uids:Array = [];
					for (var i:uint=0;i<friends.length;i++) {
						uids.push(friends[i].uid);
					}
					var call:GetInfo = new GetInfo(uids.slice(0, Math.min(uids.length, 100)), [GetInfoFieldValues.FIRST_NAME, GetInfoFieldValues.LAST_NAME]);
					call.addEventListener(FacebookEvent.COMPLETE, onFriendsGetInfo);
					facebook.post(call);
				} else {
					//An error happened so display an message to the user.
					Alert.show('Error loading friends.', 'Error');
				}
			}
			
			protected function onFriendsGetInfo(event:FacebookEvent):void {
				event.target.removeEventListener(FacebookEvent.COMPLETE, onFriendsGetInfo);
				if (event.success) {
					var dp:Array = (event.data as GetInfoData).userCollection.source;
					friendsList.dataProvider = dp;
					
					//Just select a random friend in the list to send a link to.
					var idx:Number = Math.random() * dp.length>>0;
					friendsList.selectedIndex = idx;
					callLater(friendsList.scrollToIndex, [idx]);
				} else {
					Alert.show('There was an error loading your friends details.', 'Error');
				}
			}
			
			protected function formatFriendsLabel(user:Object, ...rest):String {
				return user.first_name + ', ' + user.last_name;
			}
			
			protected function shareLink(event:Event):void {
				connect.callMethod('showShareDialog', rssList.selectedItem.link);
			}
			
			protected function postToFacebook(event:Event):void {
				var templateData:Object = {};
				templateData.postTitle = rssList.selectedItem.title;
				
				var url:String = rssList.selectedItem.link;
				templateData.pageUrl = '<a href="'+url+'">'+url+'</a>';
				
				/**
				 * showFeedDialog: Name of the connect method to call.
				 * 76726577109: Feed template id that has been created on Facebook
				 * tData: Object variables that Facebook will replace the template values with.
				 * friendsList.selectedItem.uid: The uid of the person your sharing a link with.
				 * 
				 */
				connect.callMethod('showFeedDialog', '76726577109', templateData, [friendsList.selectedItem.uid]);
			}
			
			protected function showPermissionDialog():void {
				connect.callMethod('showPermissionDialog', permsCB.selectedLabel);
			}
			
		]]>
	</mx:Script>
	
	<mx:HTTPService id="rssService" result="onServiceResult()" resultFormat="e4x" />
	<mx:HBox width="{resultsList.width}">
		<mx:HBox width="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5" borderColor="0x0" borderThickness="1" borderStyle="solid">
			<mx:Text width="325" selectable="false" text="Using Facebook Connect you can enable extended permissions for the logged in user. Select a permission value from the list and click showPermissionDialog()" />
			<mx:ComboBox id="permsCB" />
			<mx:Button label="showPermissionDialog()" click="showPermissionDialog()" />
			<mx:Spacer width="100%" />
		</mx:HBox>
	</mx:HBox>
	<mx:HBox id="resultsList">
		<mx:VBox height="100%">
			<mx:Text text="Select a friend.  Then click 'Post to Facebook' to post this to their Facebook page." selectable="false" width="200" />
			<mx:List id="friendsList" allowMultipleSelection="false" width="200" height="100%" />
		</mx:VBox>
		<mx:List id="rssList" width="450" itemRenderer="RSSRenderer" labelField="title" />
	</mx:HBox>
	
</mx:Application>